<?xml version="1.0" encoding="utf-8"?>
<!--
 Copyright (C) 2009 NagVis Project

 License:

 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License version 2 as
 published by the Free Software Foundation.

 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.

 You should have received a copy of the GNU General Public License
 along with this program; if not, write to the Free Software
 Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
-->
<EdgeBox xmlns="lib.ui.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:flexlib="http://code.google.com/p/flexlib/"
	xmlns:hc="com.hillelcoren.components.*"
	xmlns:classes="com.hillelcoren.components.autoComplete.classes.*"
	creationComplete="init()"
	contracted="addEventListener(MouseEvent.CLICK, onClick)"
	expanded="removeEventListener(MouseEvent.CLICK, onClick)"
	toolTip="Links&#13;view, edit and create links between locations"
>
	<mx:Script>
		<![CDATA[
			import modules.gmap.data.LocationsData;
			import modules.gmap.data.LinksData;
			import modules.gmap.domain.Location;
			import modules.gmap.events.LinkEvent;
			import modules.gmap.domain.Link;
			import mx.formatters.SwitchSymbolFormatter;
			import modules.gmap.events.NagiosObjectEvent;
			import mx.events.CloseEvent;
			import mx.events.MenuEvent;
			import mx.controls.Alert;
			import mx.collections.ArrayCollection;

			import com.google.maps.LatLng;

			import modules.gmap.domain.nagios.ServiceGroup;
			import modules.gmap.domain.nagios.HostGroup;
			import modules.gmap.domain.nagios.Service;
			import modules.gmap.domain.nagios.Host;

			public var hosts : ArrayCollection;
			public var hostgroups : ArrayCollection;
			public var services : ArrayCollection;
			public var servicegroups : ArrayCollection;

			[Bindable] public var links:LinksData;
			[Bindable] public var locations:LocationsData;

			private var _link : Link;

			protected function init():void
			{
				lnkLocation1.selectedItem = null;
				lnkLocation2.selectedItem = null;
			}

			public function set link(link : Link) : void
			{
				if (_link != link)
				{
					_link = link;

					if (link)
					{
						linksList.selectedItem = link;
						lnkLocation1.selectedItem = link.location1;
						lnkLocation2.selectedItem = link.location2;
						lnkDescription.text = link.description;

						if (link.id && link.id.length > 0)
						{
							addButton.visible = false;
							saveButton.visible = true;
							deleteButton.visible = true;
						}
						else
						{
							addButton.visible = true;
							saveButton.visible = false;
							deleteButton.visible = false;
						}

						/* Note:
						 *	onObjectTypeChange is called manually because Flex
						 *	does not do this when setting selectedIndex programatically
						 *	though documentation says it should
						 */
						if (link.object is HostGroup)
						{
							lnkNObjectType.selectedIndex = 3;
							onObjectTypeChange();
							lnkNObject.selectedItem = link.object;
						}
						else if (link.object is ServiceGroup)
						{
							lnkNObjectType.selectedIndex = 4;
							onObjectTypeChange();
							lnkNObject.selectedItem = link.object;
						}
						else if (link.object is Host)
						{
							lnkNObjectType.selectedIndex = 1;
							onObjectTypeChange();
							lnkNObject.selectedItem = link.object;
						}
						else if (link.object is Service)
						{
							lnkNObjectType.selectedIndex = 2;
							onObjectTypeChange();
							lnkNObject.selectedItem = link.object;
						}
						else
						{
							lnkNObjectType.selectedIndex = 0;
							onObjectTypeChange();
						}

						if (link.action && link.action.length > 0)
						{
							var slices : Array = link.action.split(':', 2);

							switch (slices[0])
							{
								case 'nagios':
									lnkAction.selectedIndex = 1;
									break;
								case 'nagvis':
									lnkAction.selectedIndex = 2;
									lnkActionMaps.text = slices[1];
									break;
								default:
									lnkAction.selectedIndex = 3;
									lnkActionURL.text = link.action;
							}

							onActionChanges();
						}
						else
						{
							lnkAction.selectedIndex = 0;
							onActionChanges();
						}
					}
					else
					{
						linksList.selectedIndex = -1;
						lnkLocation1.selectedIndex = -1;
						lnkLocation2.selectedIndex = -1;
						lnkDescription.text = "";
						lnkNObjectType.selectedIndex = 0;
						onObjectTypeChange();
						lnkAction.selectedIndex = 0;
						addButton.visible = true;
						saveButton.visible = false;
						deleteButton.visible = false;
						onActionChanges();
					}
				}
			}

			protected function getUpdatedLink() : Link
			{
				var ul : Link = new Link();

				ul.id = (_link)? _link.id : '';
				ul.id1 = lnkLocation1.selectedItem.id;
				ul.id2 = lnkLocation2.selectedItem.id;
				ul.location1 = lnkLocation1.selectedItem as Location;
				ul.location2 = lnkLocation2.selectedItem as Location;
				ul.description = lnkDescription.text;
				ul.object = lnkNObject.selectedItem;

				switch (lnkAction.selectedIndex)
				{
					case 1:
						ul.action = 'nagios:';
						break;
					case 2:
						ul.action = 'nagvis:' + lnkActionMaps.text;
						break;
					case 3:
						ul.action = lnkActionURL.text;
						break;
					default:
						ul.action = null;
				}

				return ul;
			}

			public function pushLocation(location:Location):void
			{
				lnkLocation1.selectedItem = lnkLocation2.selectedItem;
				lnkLocation2.selectedItem = location;
				onLocationChanged();
			}

			private function validate() : Boolean
			{
				if (lnkLocation1.selectedItem == lnkLocation2.selectedItem)
				{
					Alert.show("You cannot create a link between the same location");
					return false;
				}

				return true;
			}

			private function onLocationChanged():void
			{
				if(lnkLocation1.selectedItem != null && lnkLocation2.selectedItem != null)
				{
					var ul : Link = new Link();
					ul.location1 = lnkLocation1.selectedItem as Location;
					ul.location2 = lnkLocation2.selectedItem as Location;
					
					dispatchEvent(
						new LinkEvent(LinkEvent.TRYON, ul)
					); 															
				}
			}

			private function onAddClicked() : void
			{
				if (validate())
					dispatchEvent(new LinkEvent(LinkEvent.ADD, getUpdatedLink()));
			}

			private function onSaveClicked() : void
			{
				if (_link && _link.id && _link.id.length > 0 && validate())
				{
					dispatchEvent(new LinkEvent(LinkEvent.SAVE, getUpdatedLink()));
				}
			}

			private function onDeleteClicked() : void
			{
				if (_link)
				{
					Alert.show(
						'Are you sure to unlink these locations?',
						'Confirmation',
						3, this,
						deleteConfirmation
					);
				}
			}

			private function deleteConfirmation(event : CloseEvent) : void
			{
				if (event.detail == Alert.YES)
				{
					dispatchEvent(new LinkEvent(LinkEvent.DELETE, getUpdatedLink()));
				}
			}

			private function onObjectTypeChange() : void
			{
				lnkNObject.removeAll();
				lnkNObject.selectedItems.removeAll();
				lnkNObject.textInput.text = '';

				switch (lnkNObjectType.selectedIndex)
				{
					case 0:
						lnkNObject.visible = false;
						lnkNObject.dataProvider = null;
						lnkNObject.browserFields = null;
						lnkNObject.labelFunction = null;
						break;
					case 1:
						lnkNObject.visible = true;
						lnkNObject.dataProvider = hosts;
						lnkNObject.browserFields = ["name", "address", "alias"];
						lnkNObject.labelFunction = hostLabelFunction;
						break;
					case 2:
						lnkNObject.visible = true;
						lnkNObject.dataProvider = services;
						lnkNObject.browserFields = ["host", "description"];
						lnkNObject.labelFunction = serviceLabelFunction;
						break;
					case 3:
						lnkNObject.visible = true;
						lnkNObject.dataProvider = hostgroups;
						lnkNObject.browserFields = ["name", "alias"];
						lnkNObject.labelFunction = groupLabelFunction;
						break;
					case 4:
						lnkNObject.visible = true;
						lnkNObject.dataProvider = servicegroups;
						lnkNObject.browserFields = ["name", "alias"];
						lnkNObject.labelFunction = groupLabelFunction;
						break;
				}

				//AutoComplete component have some problems with positioning on Canvas
				//this is a quick hack - refactoring needed.
				lnkNObject.textInput.width = 175;				
			}

			private function loadNagiosData() : void
			{
				switch (lnkNObjectType.selectedIndex)
				{
					case 1:
						dispatchEvent(new NagiosObjectEvent(NagiosObjectEvent.NEED_ALL_HOSTS));
						break;
					case 2:
						dispatchEvent(new NagiosObjectEvent(NagiosObjectEvent.NEED_ALL_SERVICES));
						break;
					case 3:
						dispatchEvent(new NagiosObjectEvent(NagiosObjectEvent.NEED_ALL_HOSTGROUPS));
						break;
					case 4:
						dispatchEvent(new NagiosObjectEvent(NagiosObjectEvent.NEED_ALL_SERVICEGROUPS));
						break;
				}
			}

			protected function onActionChanges() : void
			{
				switch (lnkAction.selectedIndex)
				{
					case 2:
						lnkAction.setStyle('top', 30);
						lnkActionMaps.visible = true;
						lnkActionURL.visible = false;
						break;
					case 3:
						lnkAction.setStyle('top', 30);
						lnkActionMaps.visible = false;
						lnkActionURL.visible = true;
						break;
					default:
						lnkAction.setStyle('top', 50);
						lnkActionMaps.visible = lnkActionURL.visible = false;
				}
			}

			public function set nagvisMaps(maps : ArrayCollection) : void
			{
				lnkActionMaps.dataProvider = maps;
			}
		]]>
	</mx:Script>
	<mx:Script>
		<![CDATA[
			import mx.events.MenuEvent;
			import mx.events.CollectionEvent;

			/***
			 * Code concerning Nagios object autocomlete
			 ***/

			private function initNObject() : void
			{
				lnkNObject.flowBox.setStyle("borderStyle", "solid");
				lnkNObject.flowBox.setStyle("borderColor", "#000000");
				lnkNObject.flowBox.setStyle("cornerRadius", "3");
			}

			private function objectItemClick(event : MenuEvent) : void
			{
				switch (String(event.item.@data))
				{
					case "browse":

						if (lnkNObject.dataProvider.length == 0)
						{
							lnkNObject.dataProvider.addEventListener(CollectionEvent.COLLECTION_CHANGE, openBrowser);
							loadNagiosData();
						}
						else
						{
							lnkNObject.showBrowser();
						}

						break;
				}
			}

			private function openBrowser(event : *) : void
			{
				lnkNObject.dataProvider.removeEventListener(CollectionEvent.COLLECTION_CHANGE, openBrowser);
				lnkNObject.showBrowser();
			}

			public function hostLabelFunction(item : Object) : String
			{
				return item.name + ' / ' + item.address + ' (' + item.alias + ')';
			}

			public function serviceLabelFunction(item : Object) : String
			{
				return item.description + ' on ' + item.host;
			}

			public function groupLabelFunction(item : Object) : String
			{
				return item.name + ' (' + item.alias + ')';
			}
		]]>
	</mx:Script>
	<mx:Style>
		.noObject
		{
			fontWeight: bold;
		}
	</mx:Style>
	<mx:XML format="e4x" id="autoCompleteMenu">
		<root>
			<menuitem data="browse" label="Browse..."/>
		</root>
	</mx:XML>
	<mx:HBox left="10" right="15" top="10">
		<mx:Image id="logo" source="@Embed(source='modules/gmap/img/connect.png')"/>
		<mx:ComboBox id="linksList" width="100%" 
			prompt="Link - Create New" selectedIndex="-1" 
			dataProvider="{links}"
			change="dispatchEvent(new LinkEvent(LinkEvent.SELECTED, linksList.selectedItem as Link))"
		/>
		<mx:Image id="close" height="22" verticalAlign="middle"
			source="@Embed(source='modules/gmap/img/control_fastforward.png')"
			click="setCurrentState('right-contracted'); event.stopImmediatePropagation()"
		/>		
	</mx:HBox>
	<mx:Canvas left="10" right="15" top="40" bottom="10">
		<flexlib:HAccordion selectedIndex="0" left="0" right="0" top="0" bottom="30" styleName="accordion" creationPolicy="all">
			<mx:Canvas label="General" width="100%" height="100%">
				<mx:Canvas left="5" right="5" top="0" bottom="0" borderStyle="solid" cornerRadius="4" borderColor="#000000">
					<mx:Label text="Locations : "
						left="5" right="5" top="30"
					/>
					<mx:ComboBox id="lnkLocation1" dataProvider="{locations}" prompt="-" selectedIndex="-1"
						left="5" right="5" top="50"
						change="onLocationChanged()"
					/>
					<mx:ComboBox id="lnkLocation2" dataProvider="{locations}" prompt="-" selectedIndex="-1" width="100%" 
						left="5" right="5" top="80"
						change="onLocationChanged()"
					/>
				</mx:Canvas>
			</mx:Canvas>
			<mx:Canvas label="Description" width="100%" height="100%">
				<flexlib:PromptingTextArea  id="lnkDescription" prompt="Description"
					right="10" left="10" top="0" bottom="0"
				/>
			</mx:Canvas>
			<mx:Canvas label="Monitored Object" width="100%" height="100%">
				<mx:VBox left="5" right="5" top="10" bottom="0" borderStyle="solid" cornerRadius="4" borderColor="#000000" verticalAlign="middle">
					<mx:Spacer/>
					<mx:Canvas width="100%">
						<mx:Label text="Type:" left="5" right="5" top="0"/>
						<mx:ComboBox id="lnkNObjectType" left="5" right="5" top="20" change="onObjectTypeChange()">
							<mx:ArrayCollection>
								<mx:String></mx:String>
								<mx:String>Host</mx:String>
								<mx:String>Service</mx:String>
								<mx:String>Host Group</mx:String>
								<mx:String>Service Group</mx:String>
							</mx:ArrayCollection>
						</mx:ComboBox>
						<mx:Label text="Object:" left="5" right="5" top="50"/>
						<mx:Label text="None" styleName="noObject"
							left="50" right="5" top="70"
						/>
						<hc:AdvancedAutoComplete id="lnkNObject" 
							left="5" right="5" top="70" visible="false"
							creationComplete="initNObject()" focusIn="loadNagiosData()"
							selectedItemStyleName="underline" labelFunction="groupLabelFunction"
							matchType="anyPart" allowMultipleSelection="false" showRemoveIcon="true"
							actionsMenuDataProvider="{autoCompleteMenu}" itemClick="objectItemClick(event)"
							browserFields="{['name', 'alias']}"
						/>
					</mx:Canvas>
				</mx:VBox>
				<mx:TextInput cornerRadius="10" text=" Associate with" top="0" left="15" width="90" borderStyle="solid" editable="false"/>
			</mx:Canvas>
			<mx:Canvas label="Actions" width="100%" height="100%" horizontalScrollPolicy="off">
				<mx:Canvas left="5" right="0" top="10" bottom="0" borderStyle="solid" cornerRadius="4" borderColor="#000000">
					<mx:ComboBox id="lnkAction" change="onActionChanges()"
						left="5" right="5" top="50"
					>
						<mx:ArrayCollection>
							<mx:String>default action</mx:String>
							<mx:String>show object details</mx:String>
							<mx:String>open NagVis map</mx:String>
							<mx:String>go to URL</mx:String>
						</mx:ArrayCollection>
					</mx:ComboBox>
					<mx:ComboBox id="lnkActionMaps" visible="false"
						left="5" right="5" top="70"
					/>
					<mx:TextInput id="lnkActionURL" visible="false"
						left="5" right="5" top="70"
					/>
				</mx:Canvas>
				<mx:TextInput cornerRadius="10" text="On Double Click" top="0" left="15" width="100" borderStyle="solid" editable="false"/>
			</mx:Canvas>
		</flexlib:HAccordion>
		<mx:Button id="addButton" label="Connect"
			right="0" bottom="0"
			click="onAddClicked()"
		/>
		<mx:Button id="saveButton" label="Save" visible="false"
			right="0" bottom="0"
			click="onSaveClicked()"
		/>
		<mx:Button id="deleteButton" label="Disconnect" visible="false"
			right="65" bottom="0"
			click="onDeleteClicked()"
		/>
	</mx:Canvas>

</EdgeBox>
